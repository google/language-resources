# Copyright 2016, 2017 Google LLC. All Rights Reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

package(default_visibility = ["//visibility:public"])

sh_test(
    name = "ipa_phonemes_are_uniquely_decodable",
    timeout = "short",
    srcs = ["//utils:eval.sh"],
    args = [
        """
        cut -f 2 $(location phonemes.txt) |
        $(location //utils:uniquely_decodable)
        """,
    ],
    data = [
        "phonemes.txt",
        "//utils:uniquely_decodable",
    ],
)

genrule(
    name = "make_regular_lexicon",
    srcs = [
        "//third_party/nchlt_inlang:release/dictionaries/nchlt_afr.dict",
        "lex_additional.txt",
    ],
    outs = ["lex_regular.txt"],
    cmd = """
        $(location //utils:merge_lexicons) \
          $(location //third_party/nchlt_inlang:release/dictionaries/nchlt_afr.dict) \
          $(location lex_additional.txt) \
          > $@
        """,
    tools = ["//utils:merge_lexicons"],
)

genrule(
    name = "make_full_lexicon",
    srcs = [
        "lex_generated.txt",
        "lex_regular.txt",
        "lex_exceptions.txt",
    ],
    outs = ["lex_full.txt"],
    cmd = """
        $(location //utils:merge_lexicons) \
          $(location lex_generated.txt) \
          $(location lex_regular.txt) \
          $(location lex_exceptions.txt) \
          > $@
        """,
    tools = ["//utils:merge_lexicons"],
)

sh_test(
    name = "regular_lexicon_test",
    timeout = "moderate",
    srcs = ["//utils:eval.sh"],
    args = [
        """
        $(location //festus:lexicon-diagnostics) \
          --alignables=$(location alignables.txt) \
          $(location lex_regular.txt)
        """,
    ],
    data = [
        "alignables.txt",
        "lex_regular.txt",
        "//festus:lexicon-diagnostics",
    ],
)

genrule(
    name = "make_injection_fsts",
    srcs = ["alignables.txt"],
    outs = [
        "bytes_to_graphones.fst",
        "phonemes_to_graphones.fst",
    ],
    cmd = """
        $(location //festus:make-runtime-fsts) \
          --alignables=$(location alignables.txt) \
          $(location bytes_to_graphones.fst) \
          $(location phonemes_to_graphones.fst)
        """,
    tools = ["//festus:make-runtime-fsts"],
)

sh_binary(
    name = "prepare-g2p-data",
    srcs = ["prepare-g2p-data.sh"],
    data = [
        "//festus:lexicon-diagnostics",
        "//festus:make-alignable-symbols",
        "@openfst//:farcompilestrings",
    ],
)

genrule(
    name = "make_g2p_data",
    srcs = [
        "alignables.txt",
        "lex_regular.txt",
        "test_words.txt",
    ],
    outs = [
        "g2p_test.tsv",
        "g2p_train.far",
        "graphone.syms",
    ],
    cmd = """
          $(location :prepare-g2p-data) \
            $(location lex_regular.txt) \
            $(location alignables.txt) \
            $(location test_words.txt) \
            $(@D)
          """,
    tools = [":prepare-g2p-data"],
)

sh_binary(
    name = "train-graphone-model",
    srcs = ["train-graphone-model.sh"],
    data = [
        "//festus:ngramfinalize",
        "@opengrm_ngram//:ngramcount",
        "@opengrm_ngram//:ngrammake",
        "@opengrm_ngram//:ngramshrink",
    ],
)

genrule(
    name = "make_graphone_model",
    srcs = ["g2p_train.far"],
    outs = ["graphone_model.fst"],
    cmd = "$(location :train-graphone-model) $< $@",
    tools = [":train-graphone-model"],
)

genrule(
    name = "g2p_evaluation",
    srcs = [
        "bytes_to_graphones.fst",
        "g2p_test.tsv",
        "graphone_model.fst",
        "phonemes_to_graphones.fst",
    ],
    outs = ["g2p_evaluation_results.txt"],
    cmd = """
        cut -f1 $(location g2p_test.tsv) |
        $(location //festus/runtime:g2p-lookup) \
          --bytes_to_graphones=$(location bytes_to_graphones.fst) \
          --graphone_model=$(location graphone_model.fst) \
          --phonemes_to_graphones=$(location phonemes_to_graphones.fst) \
          --max_prons=1 |
        $(location //utils:eval_dictionary_simple) \
          $(location g2p_test.tsv) \
          > $@
        """,
    tools = [
        "//festus/runtime:g2p-lookup",
        "//utils:eval_dictionary_simple",
    ],
)
