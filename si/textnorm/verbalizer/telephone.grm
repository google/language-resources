# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2016 and onwards, Google, Inc.

# Sinhala verbalizer for phone numbers.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;
import 'cardinal.grm' as cardinal;
import 'digit.grm' as digit;

remove_quote = "\"" : "";
# A Sinhala "and"(යි) is used to connect numbers and digit when verbalizing.
# i.e. 342 561 -> three hunderd forty twoයි five hundred sixty one.
ins_sinhala_and = "" : "යි";

cardinal_with_leading_zeroes =
  (("0" @ digit.DIGIT) ins_sinhala_and universal_rules.ins_space<-1>)*
  cardinal.CARDINAL
;

# A group of two or four digits will be pronounced as a cardinal.
# Otherwise, pronounce them digit by digit.
# Leading zeroes will be pronounced digit by digit.
read_token =
    (universal_rules.kDigit{2,4} @ cardinal_with_leading_zeroes<-1>)
  | (digit.DIGIT ins_sinhala_and universal_rules.ins_space)* digit.DIGIT
;

# When Sinhala speakers pronounce an area code of the telephone number, they
# tend to start with a single zero and the following two digits will be
# pronounced in the way that they pronounce a cardinal.
read_first_token =
    (
      (universal_rules.kDigit{3} @ cardinal_with_leading_zeroes)
      (ins_sinhala_and universal_rules.ins_sil read_token)?
    )
  | (read_token<5>)
;

area_or_country_code =
  (
      universal_rules.telephone_part
    | (universal_rules.telephone_code ("" : "ධන "))
  )
  remove_quote
  read_first_token
  remove_quote
;

number_part =
  universal_rules.telephone_part
  remove_quote
  read_token
  remove_quote
;

phone_number = Optimize[
  area_or_country_code
  (ins_sinhala_and universal_rules.ins_sil number_part)*
];

export PHONE_NUMBER_MARKUP = Optimize[
  (
    universal_rules.telephone
    ("{" : "")
    phone_number
    universal_rules.del_space_star
    ("}" : "")
  ) @ universal_rules.CLEAN_SPACES
];
