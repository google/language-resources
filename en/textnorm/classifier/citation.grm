# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2016 and onwards, Google, Inc.

# Adds support for some common citation standards: Bible, civil code, etc.  This
# helps us avoid reading "Genesis 1:32" as "genesis twee en derting oor een" (in
# which "1:32" is incorrectly read as a time).

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

# General definitions. Using CARDINAL_DOT as this is used for all of Afrikaans,
# but should be irrelevant because there can't be any thousands separators in
# numbers with <4 digits
one_digit = (universal_rules.kDigit @ universal_rules.CARDINAL_DOT_MARKUP);
three_digits = (universal_rules.kDigit{1,3} @ universal_rules.CARDINAL_DOT_MARKUP);

# Bible citations, examples:
# "Genesis 1:1" -> "genesis één vers één"
# "Gen. 1:1-5" -> "genesis één vers één tot vyf"
# "1 Konings 2:3" -> "één konings twee vers drie"
bible_books_abbr = StringFile['bible_books.txt'];
bible_books = universal_rules.lower_case "."? @ ((bible_books_abbr ("." : "")?<-10>) |
                                      Project[bible_books_abbr, 'output']);

bible_books_markup = (bible_books @ universal_rules.word_token) universal_rules.token_delimiter;

verse_char = universal_rules.clear_space_star (":" : "vers") universal_rules.clear_space_star;
verse_markup = universal_rules.token_delimiter
               (verse_char @ universal_rules.word_token)
               universal_rules.token_delimiter;

range_char = universal_rules.clear_space_star ("-" : "tot") universal_rules.clear_space_star;
range_markup = universal_rules.token_delimiter
               (range_char @ universal_rules.word_token)
               universal_rules.token_delimiter;

# "Genesis 1:1" -> "genesis één vers één"
# "Genesis 1:1-5" -> "genesis één vers één tot vyf"
# "1 Konings 1:1" -> "één konings één vers één"
chapter_verse = one_digit?
                bible_books_markup
                universal_rules.pass_space_star
                three_digits verse_markup three_digits
                (range_markup three_digits)?;

# "Genesis 1:2-2:3" -> "genesis één vers twee tot twee vers drie"
# "1 Konings 1:1-2:2" -> "één konings één vers één tot twee vers twee"
chapter_verse_range = one_digit?
                      bible_books_markup
                      universal_rules.pass_space_star
                      three_digits verse_markup three_digits
                      range_markup
                      three_digits verse_markup three_digits;

# Add weight to chapter_verse_range so 1:2-2:3 is preferred above 1:1-5
bible = Optimize[chapter_verse | chapter_verse_range <10>];

#article_separator = universal_rules.token_delimiter ("(" : "");
#article_match = (("art." | "artikel") : "artikel") @ universal_rules.word_token;

# No civil code in Afrikaans
# article 2:26
#article = ((universal_rules.lower_case_letter | ".")* @ article_match)
#          universal_rules.token_delimiter
#          universal_rules.one_space
#          three_digits article_separator two_digits
#          (one_letter @ universal_rules.token_delimiter lseq.LSEQ)?;

#export CITATION = Optimize[bible | article];
export CITATION = Optimize[bible];
