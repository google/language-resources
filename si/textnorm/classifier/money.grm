# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
#
# Copyright 2016 and onwards, Google, Inc.

# Sinhala money classifier.

import 'universal_rules.grm' as universal_rules;
import 'universal_func.grm' as universal_func;

quantities =
    ("m" : "1000000") |
    ("bn" : "1000000000")
;

quantity = universal_func.APPLY_QUANTITY_MARKUP[quantities];

currency_names =
    (("ඇ.ඩො" | "ඩොලර්") : "usd") |
    (("ස්ටර්ලින් පවුම්" | "ස්ටර්ලිං පවුම්" | "පවුම්") : "gbp") |
    ("යුරෝ" : "eur") |
    (("රු." | "රුපියල්" | "රු") : "lkr")
;

currency_symbol_markup = Optimize[
  universal_func.InvertMarkup[universal_rules.money_currency]
  universal_rules.ins_space
  universal_rules.ins_quote
  (universal_rules.NORM_CURRENCY_SYMBOLS | (universal_rules.lower_case_anything @ currency_names))
  universal_rules.ins_quote
  universal_rules.ins_space
];

amount = (universal_rules.kDigit* @ universal_rules.AMOUNT_MARKUP @ universal_rules.REMOVE_ZEROES) |
         (universal_rules.AMOUNT_MARKUP @ universal_rules.REMOVE_ZEROES);

# In Sinhala currency name always comes before the amount.
style1 = (
    currency_symbol_markup amount quantity?
   )
   universal_func.InvertMarkup[universal_rules.style1]
;

style3 = (
     (universal_rules.ISO4217_CODE_MARKUP quantity? amount) |
     (quantity? amount universal_rules.ISO4217_CODE_MARKUP)
   )
   universal_func.InvertMarkup[universal_rules.style3]
;

export MONEY = Optimize[(style1 | style3) universal_rules.ins_space];

export MONEY_MARKUP = universal_func.APPLY_MONEY_MARKUP[MONEY];
